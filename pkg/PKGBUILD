# Ctlos Calamares
pkgname=calamares-net
pkgver=3.2.35.1
pkgrel=5
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
url="https://github.com/ctlos/calamares"
license=('LGPL')
conflicts=('calamares' 'calamares-dev')

depends=('kconfig' 'kcoreaddons' 'kiconthemes' 'ki18n' 'kio' 'solid' 'yaml-cpp' 'kpmcore' 'mkinitcpio-openswap'
         'boost-libs' 'ckbcomp' 'hwinfo' 'qt5-svg' 'polkit-qt5' 'gtk-update-icon-cache' 'plasma-framework'
         'qt5-xmlpatterns' 'squashfs-tools' 'libpwquality' 'yad' 'gtk-update-icon-cache')

makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'git' 'boost')

backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source+=("https://github.com/calamares/calamares/releases/download/v${pkgver}/calamares-${pkgver}.tar.gz"
         git+https://github.com/ctlos/calamares#branch=net)

sha256sums=('SKIP'
            'SKIP')

prepare() {
    cp -a ${srcdir}/calamares/* ${srcdir}/calamares-${pkgver}
    cp -r ${srcdir}/calamares/settings-offline.conf ${srcdir}/calamares-${pkgver}/settings.conf
    cp -r ${srcdir}/calamares/src/modules/packages/packages-offline.conf ${srcdir}/calamares-${pkgver}/src/modules/packages/packages.conf
    cp -r ${srcdir}/calamares/src/modules/welcome/welcome-offline.conf ${srcdir}/calamares-${pkgver}/src/modules/welcome/welcome.conf

    rm -rf ${srcdir}/calamares
    cd ${srcdir}/calamares-${pkgver}
    sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' CMakeLists.txt

    # change branding
    sed -i -e "s/default/ctlos/g" src/branding/CMakeLists.txt

}

build() {
    cd ${srcdir}/calamares-${pkgver}

    mkdir -p build
    cd build
        cmake .. \
              -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_INSTALL_LIBDIR=lib \
              -DWITH_KF5DBus=OFF \
              -DBoost_NO_BOOST_CMAKE=ON \
              -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg dracut dracutlukscfg \
                              dummyprocess dummypython dummycpp \
                              dummypythonqt services-openrc services-systemd \
                              keyboardq localeq welcomeq"
        make
}

package() {
    cd ${srcdir}/calamares-${pkgver}/build
    make DESTDIR="$pkgdir" install
    install -Dm644 "../data/calamares.desktop" "$pkgdir/usr/share/applications/calamares.desktop"
    install -Dm755 "../data/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
    install -Dm644 "../data/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
    chmod 750 "$pkgdir"/etc/polkit-1/rules.d

    cp -r ../settings-{on,off}line.conf $pkgdir/usr/share/calamares/
    cp -r ../src/modules/packages/packages-{on,off}line.conf $pkgdir/usr/share/calamares/modules/
    cp -r ../src/modules/welcome/welcome-{on,off}line.conf $pkgdir/usr/share/calamares/modules/


    cp -r ../src/modules/netinstall/netinstall.yaml $pkgdir/usr/share/calamares/modules/netinstall.yaml
    # cp -r ../src/modules/netinstall/netinstall-kernel.yaml $pkgdir/usr/share/calamares/modules/netinstall-kernel.yaml
    # cp -r ../src/modules/netinstall/netinstall-desktop.yaml $pkgdir/usr/share/calamares/modules/netinstall-desktop.yaml
    # cp -r ../src/modules/netinstall/netinstall-applications.yaml $pkgdir/usr/share/calamares/modules/netinstall-applications.yaml
}
