# Ctlos Calamares
pkgname=calamares
pkgver=3.2.4
pkgrel=1.2
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/ctlos/ctlos-calamares"
license=('LGPL')
depends=('kconfig' 
         'kcoreaddons' 
         'kiconthemes' 
         'ki18n'
         'kio' 
         'solid' 
         'yaml-cpp' 
         'kpmcore>=3.3.0' 
         'squashfs-tools'
         'mkinitcpio-openswap'
         'boost-libs' 
         'ckbcomp' 
         'hwinfo' 
         'qt5-svg' 
         'polkit-qt5' 
         'gtk-update-icon-cache'  
         'plasma-framework')

makedepends=('extra-cmake-modules' 
             'qt5-tools' 
             'qt5-translations' 
             'git' 
             'boost')

source+=("https://github.com/calamares/calamares/releases/download/v${pkgver}/calamares-${pkgver}.tar.gz"
         git+https://github.com/ctlos/ctlos-calamares#branch=master)

sha256sums=('cb19a8a0f6f1a4d5d2b021eac5b10ceb600dbb59badc8b99e8b69559b90a5973'
            'SKIP')

prepare() {
    cp -a ${srcdir}/ctlos-calamares/* ${srcdir}/calamares-${pkgver}
    rm -rf ${srcdir}/ctlos-calamares
    cd ${srcdir}/calamares-${pkgver}
    sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' CMakeLists.txt
}

build() {
    cd ${srcdir}/calamares-${pkgver}

    mkdir -p build
    cd build
        cmake .. \
              -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_INSTALL_LIBDIR=lib \
              -DWITH_PYTHONQT:BOOL=OFF \
              -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg dracut dracutlukscfg \
                              dummyprocess dummypython dummycpp \
                              dummypythonqt services-openrc services-systemd"
        make
}

package() {
    cd ${srcdir}/calamares-${pkgver}/build
    make DESTDIR="$pkgdir" install
    install -Dm644 "../calamares.desktop" "$pkgdir/usr/share/applications/calamares.desktop"
    install -Dm644 "../calamares.png" "$pkgdir/usr/share/icons/calamares.png"
	install -Dm755 "../data/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
    install -Dm644 "../data/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
    chmod 750 "$pkgdir"/etc/polkit-1/rules.d
}
